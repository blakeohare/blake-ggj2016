FPS = 30;
SCREEN_WIDTH = 960;
SCREEN_HEIGHT = 600;

PI = 3.14159265358979;
TWO_PI = 2 * PI;

SPEAR_POWER_RATIO = 10;

CUTSCENE_SPEED_RATIO = 3;

ENABLE_BOTH = true;

VILLAGER_COUNT = 12;

import 'enums.cry';
import 'util.cry';
import 'Event.cry';
import 'GameContext.cry';
import 'ImageLibrary.cry';
import 'SoundManager.cry';
import 'TextEngine.cry';

import 'village/init.cry';

import 'scenes/AbstractScene.cry';

import 'editor/init.cry';
import 'scenes/init.cry';

function main() {
	$game_initialize(FPS);
	$game_initialize_screen(SCREEN_WIDTH, SCREEN_HEIGHT);
	$game_set_title("Wizards & Tropical Volcano Monsters");
	
	// Device ID's don't really matter. They're opaque strings that are allocated to player ID's and there's a lookup table in GameContext.
	ids = [];
	for (i = 1; i <= (VILLAGER_COUNT + 3); ++i) {
		ids.add('G' + i);
	}
	$gamepad_enable_recent(ids);
	
	CONTEXT.refresh_player_ids();
	
	events = [];
	
	active_scene = new LoadingScene();
	
	rc = 0;
	mx = 0;
	my = 0;
	
	while (true) {
		events.clear();
		for (event : $game_pump_events()) {
			if (event.is_quit) {
				return;
			}
			switch (event.type) {
				case 'mousemove':
				case 'mouseleftdown':
				case 'mouseleftup':
					mx = event.x;
					my = event.y;
					break;
				default:
					break;
			}
			
			events.add(event);
		}
		
		active_scene.update(events, mx, my);
		if (active_scene._quit_requested) {
			return;
		}
		
		if (active_scene._next != null) {
			next = active_scene._next;
			active_scene._next = null;
			active_scene = next;
			active_scene._next = null;
		}
		
		active_scene.render(rc++);
		
		SOUNDS.update();
		
		$game_clock_tick();
	}
}

main();