class GamepadConfigScene : AbstractScene {
	constructor() : base() {
		this.devices = [];
		this.titles = [];
		this.device_ids = [];
		
		this.anim_counter = 0;
		
		device_count = $gamepad_count();
		for (i = 0; i < device_count; ++i) {
			device = $gamepad_get_device(i);
			this.devices.add(device);
			this.titles.add(device.name);
		}
		this.refresh_player_ids();
		
		this.current_index = 0;
	}
	
	function refresh_player_ids() {
		this.players = [];
		for (dev : this.devices) {
			this.device_ids.add(dev.id);
		}
		
		CONTEXT.refresh_player_ids();
	}
	
	function update(events, mx, my) {
		old = this.current_index;
		enter_pressed = false;
		for (event : events) {
			switch (event.type) {
				case 'keydown':
					if (event.key == 'up') {
						this.current_index--;
					} else if (event.key == 'down') {
						this.current_index++;
					} else if (event.key == 'enter') {
						enter_pressed = true;
					}
					break;
			}
		}
		
		if (this.current_index < 0) this.current_index = 0;
		else if (this.current_index > this.devices.length) this.current_index = this.devices.length;
		
		if (old != this.current_index) {
			this.anim_counter = 0;
		}
		
		this.anim_counter++;
		
		if (enter_pressed) {
			i = this.current_index;
			if (i == 0) {
				this.switch_scene(new TitleScene());
			} else {
				i--;
				device = this.devices[i];
				
				$gamepad_clear_bindings(device);
				device_id = 'P' + $floor($random() + 2000000000);
				$gamepad_set_id(device, device_id);
				this.switch_scene(new GamepadBindScene(this, device, this.titles[i]));
			}
		}
	}
	
	function render(rc) {
		y = 50;
		for (ii = 0; ii < this.devices.length; ++ii) {
			x = 50;
			if (ii == this.current_index) {
				cx = x - 30;
				cy = y + 20 - $floor($abs($sin(this.anim_counter * 2 * 3.14159 * 1.3 / FPS) * 20));
				$gfx_draw_rectangle(cx, cy, 15, 15, 255, 255, 255, 255);
			}
			if (ii == 0) {
				TEXT.render('white-M', 'Back to main menu', x, y);
			} else {
				i = ii - 1;
				
				title = this.titles[i];
				device_id = this.device_ids[i];
				player = CONTEXT.config_id_to_game_id.get(device_id, null);
				coord = TEXT.render('white-M', '#' + (i + 1) + ': ', x, y);
				x = coord[0];
				if (player == 'M') {
					coord = TEXT.render('red-M', 'Monster', x, y);
				} else if (player != null && player[0] == 'V' && player.length == 2) {
					coord = TEXT.render('blue-M', 'Villager ' + player[-1], x, y);
				} else {
					coord = TEXT.render('yellow-M', '(?)', x, y);
				}
				
				coord = TEXT.render('white-M', '#' + (i + 1) + ': ' + title, coord[0], y);
			}
			y += 50;
		}
	}
}